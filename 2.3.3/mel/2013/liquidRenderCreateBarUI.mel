// extentions from maya2012/scripts/others/renderCreateBarUI.mel
// these functions are called from liquid/mel/2012/renderCreateBarUI.mel
source xxliquidutility.mel;

global proc LIQUID_buildRenderNodeTreeListerContent(string $renderNodeTreeLister, string $postCommand, string $filterClassArray[])
{
	liqPushIdent();
	liqlog("LIQUID_buildRenderNodeTreeListerContent("+$renderNodeTreeLister+","+$postCommand+", ["+stringArrayToString($filterClassArray,",")+"])");


	if (`pluginInfo -query -loaded (liquidGetPluginName())`)
	{
		liqRenderNode_CreateTreeLister_Content($renderNodeTreeLister, $postCommand, $filterClassArray, liq_getRendererName());
	}

	liqRenderNode_CreateTreeLister_Content_subrenderer($renderNodeTreeLister, $postCommand, $filterClassArray);

    liqPopIdent();
}
//
global proc string[] LIQUID_excludeClassficationNodes(string $classification)
{
	liqPushIdent();
	liqlog("LIQUID_getClassficationNodes("+$classification+")");

	string $exclude[];

	string $liquidClassfication = "rendernode/"+liq_getRendererName()+"/shader";
	if( !startsWith($classification, $liquidClassfication) )
	{
		string $exclNodes[];
		string $tmp[] = `listNodeTypes $liquidClassfication`;
		$exclude = stringArrayCatenate($exclude, $tmp);
		$exclude = stringArrayRemoveDuplicates($exclude);
	}
	
	$exclude = LIQUID_excludeClassficationNodes_subrenderer($classification, $exclude);

    liqPopIdent();
	return $exclude;
}
//
global proc int LIQUID_isLiquidClassification(string $classification)
{
	liqPushIdent();
	liqlog("LIQUID_isLiquidClassification("+$classification+")");

	int $processed = 0;// $classification is processed by liquid or not

	string $liquidClassfication = "rendernode/"+liq_getRendererName();
	if( startsWith($classification, $liquidClassfication) )
	{
		liqPopIdent();
		return 1;
	}

	$processed = LIQUID_isLiquidClassification_subrenderer($classification);
	
    liqPopIdent();
	return $processed;
}
//
global proc string LIQUID_getCreateBarItemCallbackCmd(
	string $classification,
	string $as,
	string $postCommand,
	string $type
	)
{
	liqPushIdent();
	liqlog("LIQUID_getCreateBarItemCallbackCmd("+$classification+","+$as+","+$postCommand+","+$type+")");

	string $command;

	if( LIQUID_isLiquidClassification($classification) )
	{
		$command = 
				("liqCreateCustomNode"
					+ " "
					+ $as 
					+ " \""
					+ encodeString($postCommand)
					+ "\" "
					+ $type
					+ " \""+liq_getRendererName()+"\" "
					);
	}
	else if( LIQUID_isLiquidClassification_subrenderer($classification) )
	{
		$command = LIQUID_getCreateBarItemCallbackCmd_subrenderer($classification, $as, $postCommand, $type);
	}
	
    liqPopIdent();
	return $command;
}