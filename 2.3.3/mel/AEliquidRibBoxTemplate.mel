source xxliquidutility.mel;

global proc liquidRibBoxEditorUpdateMenu( string $msg )
{
  liqPushIdent();
	liqlog("liquidRibBoxEditorUpdateMenu("+$msg+")");
  int $en = (`editRenderLayerGlobals -q -crl` == "defaultRenderLayer")? 0 : 1;
  menuItem -e -c ("editRenderLayerAdjustment "+$msg) -en $en liquidRibboxEditorOverrideMenuItem;

  liqPopIdent();
}

global proc liquidRibBoxEditorNew( string $nodeName, string $msg )
{
  liqPushIdent();
  liqlog("liquidRibBoxEditorNew(...)");

  setUITemplate -pst attributeEditorTemplate;
  scrollField -wordWrap false -font smallFixedWidthFont -h 250 liquidRibboxEditorCtl;
  popupMenu liquidRibboxEditorOverrideMenu;
  menuItem -l "Create Layer Override" liquidRibboxEditorOverrideMenuItem;
  setUITemplate -ppt;

  liquidRibBoxEditorReplace( $nodeName, $msg );

  liqPopIdent();
}

global proc liquidRibBoxEditorReplace( string $nodeName, string $msg )
{
  liqPushIdent();
	liqlog("liquidRibBoxEditorReplace(...)");
  scrollField -e
              -tx (`getAttr $msg`)
              -cc ("{string $s = `scrollField -q -tx liquidRibboxEditorCtl`;setAttr -type \"string\" "+$msg+" $s;}")
              -ec ("{string $s = `scrollField -q -tx liquidRibboxEditorCtl`;setAttr -type \"string\" "+$msg+" $s;}")
              liquidRibboxEditorCtl;

  popupMenu -e -pmc ("liquidRibBoxEditorUpdateMenu "+$msg) liquidRibboxEditorOverrideMenu;

  liqPopIdent();
}




global proc AEliquidRibBoxTemplate( string $nodeName )
{
  liqPushIdent();
  liqAElog("AEliquidRibBoxTemplate("+$nodeName+")");

  AEswatchDisplay $nodeName;

  editorTemplate -beginScrollLayout;

    editorTemplate -suppress "rmanShader";
    editorTemplate -suppress "rmanShaderLong";

    editorTemplate -ccu ("liquidRibBoxEditorNew "+$nodeName)  ("liquidRibBoxEditorReplace "+$nodeName) "ribbox";

    editorTemplate -addExtraControls;

  editorTemplate -endScrollLayout;

  liqPopIdent();
}

