
global string $MESH_PATH = "";

proc string as_get_export_mesh_to()
{
	liqPushIdent();
	liqlog("as_get_export_mesh_to()");
	
	global string $gGlobalNode_appleseed;

	string $export_mesh_to = `getAttr ($gGlobalNode_appleseed+".export_mesh_to")`;

	liqPopIdent();
	return $export_mesh_to;
}
// \p $meshFullPathName   e.g. |pCube1|pCubeShape1
// \r                     e.g. %root%/pCube1/pCubeShape1.obj
global proc string as_get_filesys_fullPathName(string $meshFullPathName)
{
	liqPushIdent();
	liqlog("as_get_filesys_fullPathName("+$meshFullPathName+")");

	string $root = as_get_export_mesh_to();
	if( size($root)==0 ){
		$root = `workspace -q -fullName`;
	}

	string $fullPathName = $root + substituteAllString($meshFullPathName, "|", "/") +".obj";
	liqPopIdent();
	return $fullPathName;
}
// \p $meshFullPathName   e.g. |pCube1|pCubeShape1
// \r                     e.g. %root%/pCube1
global proc string as_get_mesh_dirname(string $meshFullPathName)
{
	liqPushIdent();
	liqlog("as_get_mesh_dirname("+$meshFullPathName+")");

	string $fullPathName = as_get_filesys_fullPathName($meshFullPathName);
	string $dir = `dirname $fullPathName`;

	liqPopIdent();
	return $dir;
}
// \p $meshFullPathName   e.g. |pCube1|pCubeShape1
// \r                     e.g. pCubeShape1.obj
global proc string as_get_mesh_basename(string $meshFullPathName)
{
	liqPushIdent();
	liqlog("as_get_mesh_basename("+$meshFullPathName+")");

	string $fullPathName = as_get_filesys_fullPathName($meshFullPathName);
	string $basename = basename( $fullPathName, "" );
	
	liqPopIdent();
	return $basename;
}
//
global proc as_export(string $meshFullPathName)
{
	liqPushIdent();
	liqlog("as_export("+$meshFullPathName+")");

	string $fullPathName = as_get_filesys_fullPathName( $meshFullPathName );
	string $dir          = as_get_mesh_dirname( $meshFullPathName );
	if( `filetest -d $dir` == 0)
	{
		sysFile -makeDir $dir;
	}
	//select the mesh
	select -r $meshFullPathName;
	//export the selected mesh
	file 
		-force 
		-options "groups=1;ptgroups=1;materials=1;smoothing=1;normals=1" 
		-type "OBJexport"
		-preserveReferences
		-exportSelected
		-es $fullPathName;

	liqPopIdent();
}
